buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
    }
}

apply from: "properties.gradle"

evaluationDependsOn(":MovingWorld")

apply plugin: "net.minecraftforge.gradle"
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: "eclipse"

def branch
if (System.env.BRANCH_NAME) {
    // Jenkins support
    branch = System.env.BRANCH_NAME
    branch = branch.substring(branch.lastIndexOf('/') + 1)
} else {
    branch = 'git rev-parse --abbrev-ref HEAD'.execute().in.text.trim()
}
def commits = 'git rev-list --count HEAD'.execute().in.text.trim()
def dirty = !'git diff-index HEAD'.execute().in.text.trim().isEmpty()

version = branch + '-' + modVersion + '.' + commits + (dirty ? '-dirty' : '')
group = modGroup
archivesBaseName = modBaseName

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: "snapshot", version: mappingsVersion

    runs {
        client {
            workingDirectory project.file("run")

            // Recommended logging data for a userdev environment
            property "forge.logging.markers", "REGISTRIES"

            // Recommended logging level for the console
            property "forge.logging.console.level", "debug"

            mods {
                davincisvessels {
                    sources sourceSets.main
                }
                movingworld {
                    sources project(":MovingWorld").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")

            // Recommended logging data for a userdev environment
            property "forge.logging.markers", "REGISTRIES"

            // Recommended logging level for the console
            property "forge.logging.console.level", "debug"

            mods {
                davincisvessels {
                    sources sourceSets.main
                }
                movingworld {
                    sources project(":MovingWorld").sourceSets.main
                }
            }
        }
    }
}

configurations {
    runtime.exclude group: "com.tridevmc.MovingWorld"
}

dependencies {
    compileOnly project(":MovingWorld")

    minecraft "net.minecraftforge:forge:${forgeVersion}"

    compile "com.tridevmc.compound:compound-all:${compoundVersion}"
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'https://repo.tridevmc.com/'
    }
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes(["Specification-Title"     : "Davincis Vessels",
                    "Specification-Vendor"    : "Trident",
                    "Specification-Version"   : "24.0", // We are version 1 of the modlauncher specification
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${version}",
                    "Implementation-Vendor"   : "Trident",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}
